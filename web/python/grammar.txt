?start: function_definition+

?function_definition: ("def"i | "int"i) NAME "(" args ")" "{" inline_block+ "}" -> function_declaration

?inline_block: statements
    | block ";" -> block

?statements: "for"i "(" assigns ";" compares ";" assigns ")" "{" inline_block+ "}" -> for_loop
    | "while"i "(" compares ")" "{" inline_block+ "}" -> while_loop
    | "if"i "(" compares ")" "{" inline_block+ "}" elif_statement* else_statement? -> if_statement
    | "do"i "{" inline_block+ "}" "while"i "(" compares ")" ";" -> do_while_loop

?elif_statement: "elif"i "(" compares ")" "{" inline_block+ "}" -> elif_statement

?else_statement: "else"i "{" inline_block+ "}" -> else_statement

?args: (sum) ("," (sum))* -> args
    | -> empty_args

?block: assigns
    | "return"i return_args -> _return

?return_args: sum ("," sum)* -> return_args
    | -> empty_return_args

?assigns: NAME "=" sum -> assign_var
    | NAME "+=" sum -> add_assign_var
    | NAME "-=" sum -> sub_assign_var
    | NAME "*=" sum -> mul_assign_var
    | NAME "/=" sum -> div_assign_var
    | NAME "++" -> increment
    | NAME "--" -> decrement
    | list_assign "=" sum (","sum)* -> multi_assign_var
    | function_call

?list_assign: NAME (","NAME)+

?compares: sum "==" sum -> compare_equal
    | sum "!=" sum -> compare_not_equal
    | sum ">=" sum -> compare_greater_equal
    | sum "<=" sum -> compare_less_equal
    | sum ">" sum -> compare_greater
    | sum "<" sum -> compare_less
    | compares ("&&"|"and"i) compares -> and_compare
    | compares ("||"|"or"i) compares -> or_compare
    | "(" compares ")"
    | sum -> zero_compare

?sum: product
    | sum "+" product -> add
    | sum "-" product -> sub

?product: atom
    | product "*" atom -> mult
    | product "/" atom -> div
    | product "%" atom -> quot
    | product "&" atom -> bit_and
    | product "|" atom -> bit_or
    | product "^" atom -> bit_xor

?atom: NUMBER -> number
    | HEX -> hex_number
    | NAME -> var
    | function_call
    | "~" atom -> bit_not
    | "(" sum ")"
    | "-" atom -> negative

?function_call: NAME "(" args ")" -> function_call

%import common.CNAME -> NAME
%import common.NUMBER
%import common.WS_INLINE
%import common.NEWLINE
%import python.HEX_NUMBER -> HEX
%ignore WS_INLINE
%ignore NEWLINE